buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.0"
    }
}

apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'eu.sec-cert'
version = '1.0.8'

dependencies {
    compile 'io.swagger:swagger-annotations:1.5.17'
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile 'io.swagger:swagger-annotations:1.5.17'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.squareup.okhttp:logging-interceptor:2.7.5'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'io.gsonfire:gson-fire:1.8.0'
    compile 'org.threeten:threetenbp:1.3.5'
    testCompile 'junit:junit:4.12'
}

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'continuous-auditing-api-java-client'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'API Client for EU-SEC Continuous Auditing API'
                description = 'This is the API client for the EU_SEC Continuous Auditing API'
                url = 'https://www.sec-cert.eu'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'oxisto'
                        name = 'Christian Banse'
                        email = 'christian.banse@aisec.fraunhofer.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:eu-sec/continuous-auditing-api-java-client.git'
                    developerConnection = 'scm:git:ssh://github.com:eu-sec/continuous-auditing-api-java-client.git'
                    url = 'https://github.com/eu-sec/continuous-auditing-api-java-client'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = deployUsername
                password = deployPassword
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

nexusStaging {
    username = deployUsername
    password = deployPassword
}
